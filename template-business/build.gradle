buildscript {
    configurations.classpath {
        resolutionStrategy.activateDependencyLocking()
    }
}

plugins {
    id 'java-library'
    id 'pmd'
    id 'jacoco'
}

dependencyLocking {
    lockAllConfigurations()
}

apply from: '../group-version.gradle'
apply from: '../compatibility.gradle'
apply from: '../repositories.gradle'
apply from: '../common-dependencies.gradle'

compileJava {
    doFirst {
        classpath += sourceSets.main.compileClasspath

        options.compilerArgs = [
                '--module-path', classpath.asPath,
                '--add-modules', 'ALL-SYSTEM',
                '-d', sourceSets.main.output.classesDirs.asPath
        ]
    }
}

java {
    withJavadocJar()
    withSourcesJar()
}

pmd {
    consoleOutput = true
    toolVersion = "7.7.0"
    rulesMinimumPriority = 5
    ruleSetFiles = files('../quality/pmd/java/custom.xml')
    ruleSets = []
}

pmdTest.enabled = false

test {
    dependsOn(pmdMain)
    finalizedBy(jacocoTestReport)
}

task testsJar(type: Jar, dependsOn: [testClasses]) {
    archiveClassifier.set('tests')
    from sourceSets.test.output
}

jar.finalizedBy(javadocJar, sourcesJar, testsJar)

javadoc {
    if(JavaVersion.current().isJava9Compatible()) {
        options.addBooleanOption('html5', true)
    }
}